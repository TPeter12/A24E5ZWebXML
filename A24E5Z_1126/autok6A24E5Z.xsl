<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
  
  <xsl:key name="varosKey" match="auto" use="tulaj/varos"/>
  
  <xsl:output method="html" indent="yes"/>
  
  <xsl:template match="/">
    <html><body>
        <h2>Autók városonként - darabszám és összár</h2>
        
        <table border="1">
          <tr><th>Város</th><th>Darabszám</th><th>Összár</th></tr>
          
          <xsl:for-each select="autok/auto[generate-id() = generate-id(key('varosKey', tulaj/varos)[1])]">
            <tr>
              <td><xsl:value-of select="tulaj/varos"/></td>
              <td><xsl:value-of select="count(key('varosKey', tulaj/varos))"/></td>
              <td>
                <xsl:value-of select="sum(key('varosKey', tulaj/varos)/ar)"/>
              </td>
            </tr>
          </xsl:for-each>
          
        </table>
        
      </body></html>
  </xsl:template>
  
</xsl:stylesheet>
